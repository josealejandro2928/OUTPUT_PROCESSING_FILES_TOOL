package Waterways_UxAS
public
	with CMASI;
	
	-- Included Main Services
	with AssignmentTreeBranchBoundService_Pkg;
	with AutomationRequestValidatorService_Pkg;	
	with PlanBuilderService_Pkg;
	with WaypointPlanManagerService_Pkg;
    with RouteAggregatorService_Pkg;
	with RoutePlannerVisibilityService_Pkg;
	with TaskManagerService_Pkg;
	with CmasiLineSearchTaskService_Pkg;
	with SendMessagesService_Pkg;
	
	with UxAS;

	with Base_Types;
	with Data_Types;
	with Agree_Constants;
	with Agree_Nodes;
	with CASE_Consolidated_Properties;
			
	thread group Waterways extends UxAS::UxAS
		features
			AutomationRequest : in event data port CMASI::AutomationRequest.i;
			AirVehicleState : in event data port CMASI::AirVehicleState.i;
			OperatingRegion : in event data port CMASI::OperatingRegion.i;
			LineSearchTask: in event data port CMASI::LineSearchTask.i;
			MissionCommand : out event data port CMASI::MissionCommand.i;
			AutomationResponse : out event data port CMASI::AutomationResponse.i;			
		annex Verdict{**
			CyberRel "AutomationResponse_C" = AutomationRequest:C or LineSearchTask:C or OperatingRegion:C => AutomationResponse:C;
			CyberRel "AutomationResponse_I" = AutomationRequest:I or LineSearchTask:I or OperatingRegion:I  => AutomationResponse:I;
			CyberRel "AutomationResponse_A" = AutomationRequest:A or LineSearchTask:A or OperatingRegion:A => AutomationResponse:A;
		**};
	end Waterways;
	
	thread group implementation Waterways.i 
		subcomponents
		
		  -- https://github.com/afrl-rq/OpenUxAS/blob/develop/examples/02_Example_WaterwaySearch/cfg_WaterwaySearch.xml
		  -- Removed: Test_SimulationTime
		  TaskManagerServiceAM: thread TaskManagerService_Pkg::TaskManagerService.i; --AM renamed
		  AutomationRequestValidatorServiceAM: thread AutomationRequestValidatorService_Pkg::AutomationRequestValidatorService.i; --AM renamed
		  RoutePlannerVisibilityServiceAM: thread RoutePlannerVisibilityService_Pkg::RoutePlannerVisibilityService.i; --AM renamed
		  RouteAggregatorServiceAM: thread RouteAggregatorService_Pkg::RouteAggregatorService.i; --AM renamed
		  AssignmentTreeBranchBoundServiceAM: thread AssignmentTreeBranchBoundService_Pkg::AssignmentTreeBranchBoundService.i; --AM renamed
		  PlanBuilderServiceAM: thread PlanBuilderService_Pkg::PlanBuilderService.i; --AM renamed
		  WaypointPlanManagerServiceAM: thread WaypointPlanManagerService_Pkg::WaypointPlanManagerService.i; --AM renamed      
		  CmasiLineSearchTaskServiceAM: thread CmasiLineSearchTaskService_Pkg::CmasiLineSearchTaskService.i; --AM renamed
		  SendMessagesServiceMemoryAM: thread SendMessagesService_Pkg::SendMessagesServiceMemory.i; --AM renamed
		  SendMessagesServiceInitAM: thread SendMessagesService_Pkg::SendMessagesServiceInit.i; --AM renamed
		  
		connections
			
			------------------------------------------------------------------------------------------------------------------
			-- Connections between the ground station (or controller) and the UxAS instances, and connections between UxAS
			-- services. The connections reflect the message sequence flow defined for this example. The message sequence flow 
			-- has 4 states: Initialization, Assignment, Implementation, and Execution. The connections are group and labeled 
			-- based on the state(s) in which the messages occur.
			------------------------------------------------------------------------------------------------------------------
			
			------------------
			-- Primary Inputs
			------------------
		
			c_AVSt_06 : port AirVehicleState -> WaypointPlanManagerServiceAM.AirVehicleState;
			c_AVSt_07 : port AirVehicleState -> RoutePlannerVisibilityServiceAM.AirVehicleState;
			c_AVSt_08 : port AirVehicleState -> PlanBuilderServiceAM.AirVehicleState;
			c_AVSt_09 : port AirVehicleState -> TaskManagerServiceAM.AirVehicleState;
			c_AVSt_10 : port AirVehicleState -> AutomationRequestValidatorServiceAM.AirVehicleState;
			c_AVSt_11 : port AirVehicleState -> RouteAggregatorServiceAM.AirVehicleState;
			c_AVSt_12 : port AirVehicleState -> CmasiLineSearchTaskServiceAM.AirVehicleState;
			
			c_AReq_01 : port AutomationRequest -> AutomationRequestValidatorServiceAM.AutomationRequest;
            	          
            c_LST_01 : port LineSearchTask -> TaskManagerServiceAM.LineSearchTask;
            
            c_OR_01 : port OperatingRegion -> AutomationRequestValidatorServiceAM.OperatingRegion;
            c_OR_02 : port OperatingRegion -> RoutePlannerVisibilityServiceAM.OperatingRegion;
             
            -------------------------------------
            -- AutomationRequestValidatorService
            -------------------------------------
            c_UARsp_01 : port AutomationRequestValidatorServiceAM.UniqueAutomationRequest -> RouteAggregatorServiceAM.UniqueAutomationResquest;           
            c_UARsp_02 : port AutomationRequestValidatorServiceAM.UniqueAutomationRequest -> AssignmentTreeBranchBoundServiceAM.UniqueAutomationRequest;
            c_UARsp_03 : port AutomationRequestValidatorServiceAM.UniqueAutomationRequest -> PlanBuilderServiceAM.UniqueAutomationRequest;                       
            c_UARsp_04 : port AutomationRequestValidatorServiceAM.UniqueAutomationRequest -> CmasiLineSearchTaskServiceAM.UniqueAutomationRequest;
            -- EGM: All the sequence charts in the UxAS docks show the below connection, but it is _not_ in the code.
            -- The TaskManagerService does _not_ subscribe to UniqueAutomationRequest though it does send that message.
            -- c_UAR_05 : port AutomationRequestValidatorService.UniqueAutomationRequest -> TaskManagerService.UniqueAutomationRequest;
            
            c_ARsp_01 : port AutomationRequestValidatorServiceAM.AutomationResponse -> AutomationResponse;
            c_ARps_02 : port AutomationRequestValidatorServiceAM.AutomationResponse -> WaypointPlanManagerServiceAM.AutomationResponse;
                       
            -----------------------
            -- CmasiLineSearchTask
            -----------------------
            c_TPOpts_01 : port CmasiLineSearchTaskServiceAM.TaskPlanOptions -> RouteAggregatorServiceAM.TaskPlanOptions;
            c_TPOpts_02 : port CmasiLineSearchTaskServiceAM.TaskPlanOptions -> AssignmentTreeBranchBoundServiceAM.TaskPlanOptions;
            
            c_TIRsp_01 : port CmasiLineSearchTaskServiceAM.TaskImplementationResponse -> PlanBuilderServiceAM.TaskImplementationResponse;
            
            c_CLST_RPReq_01 : port CmasiLineSearchTaskServiceAM.RoutePlanRequest -> RoutePlannerVisibilityServiceAM.RoutePlanRequest_task;
            
            -------------------------
            -- RouteAggregatorService
            -------------------------
			c_RPReq_01 : port RouteAggregatorServiceAM.RoutePlanRequest -> RoutePlannerVisibilityServiceAM.RoutePlanRequest_ragg;
        
            c_ACMtx_01 : port RouteAggregatorServiceAM.AssignmentCostMatrix -> AssignmentTreeBranchBoundServiceAM.AssignmentCostMatrix;
                             
			---------------------------------------
			-- AssignmentTreeBranchAndBoundService
			---------------------------------------
			c_TASum_01 : port AssignmentTreeBranchBoundServiceAM.TaskAssignmentSummary -> PlanBuilderServiceAM.TaskAssignmentSummary;
       		     		
        	-------------------
       		-- PlanBuilderService
       		-------------------
       		c_PBS_TIReq_01: port PlanBuilderServiceAM.TaskImplementationRequest -> CmasiLineSearchTaskServiceAM.TaskImplementationRequest;
  		
  			c_PBS_UARsp_01 : port PlanBuilderServiceAM.UniqueAutomationResponse -> AutomationRequestValidatorServiceAM.UniqueAutomationResponse;
			c_PBS_UARsp_02 : port PlanBuilderServiceAM.UniqueAutomationResponse -> CmasiLineSearchTaskServiceAM.UniqueAutomationResponse;
					
			---------------------------------
			-- RoutePlannerVisibilityService
			---------------------------------
		    c_RPVS_RPR_01: port RoutePlannerVisibilityServiceAM.RoutePlanResponse -> CmasiLineSearchTaskServiceAM.RoutePlanResponse;
		    c_PRVS_RPR_02: port RoutePlannerVisibilityServiceAM.RoutePlanResponse -> RouteAggregatorServiceAM.RoutePlanResponse;
		      
       		--------------------------
       		-- WaypointManagerService
       		--------------------------
       		c_MCmd_01 : port WaypointPlanManagerServiceAM.MissionCommand -> MissionCommand;
       		
		    ----------------------
		    -- TaskManagerService
		    ----------------------
		    c_TMSrvc_CNT_01: port TaskManagerServiceAM.CreateNewService -> CmasiLineSearchTaskServiceAM.createNewService;
		    
		    ----------------------------
		    -- SendMessageServiceMemory
		    ----------------------------
		    c_SMS_AVCfg_01 : port SendMessagesServiceMemoryAM.AirVehicleConfiguration -> RouteAggregatorServiceAM.AirVehicleConfiguration;
            c_SMS_AVCfg_02 : port SendMessagesServiceMemoryAM.AirVehicleConfiguration -> RoutePlannerVisibilityServiceAM.AirVehicleConfiguration;
            c_SMS_AVCfg_03 : port SendMessagesServiceMemoryAM.AirVehicleConfiguration -> TaskManagerServiceAM.AirVehicleConfiguration;
            c_SMS_AVCfg_04 : port SendMessagesServiceMemoryAM.AirVehicleConfiguration -> AutomationRequestValidatorServiceAM.AirVehicleConfiguration;
            c_SMS_AVCfg_05 : port SendMessagesServiceMemoryAM.AirVehicleConfiguration -> CmasiLineSearchTaskServiceAM.AirVehicleConfiguration;
  
            c_SMS_KIZ_01 : port SendMessagesServiceMemoryAM.KeepInZone -> AutomationRequestValidatorServiceAM.KeepInZone;
            c_SMS_KIZ_02 : port SendMessagesServiceMemoryAM.KeepInZone -> RoutePlannerVisibilityServiceAM.KeepInZone;
            
            c_SMS_KOZ_01 : port SendMessagesServiceMemoryAM.KeepOutZone -> AutomationRequestValidatorServiceAM.KeepOutZone;
            c_SMS_KOZ_02 : port SendMessagesServiceMemoryAM.KeepOutZone -> RoutePlannerVisibilityServiceAM.KeepOutZone;
            
            ----------------------------
            -- SendMessageServiceInit
            ----------------------------
            c_SMSI_MC_01 : port SendMessagesServiceInitAM.MissionCommand -> MissionCommand;
            
            c_SMSI_AVSt_01 : port SendMessagesServiceInitAM.AirVehicleState -> WaypointPlanManagerServiceAM.AirVehicleState;
            c_SMSI_AVSt_02 : port SendMessagesServiceInitAM.AirVehicleState -> RoutePlannerVisibilityServiceAM.AirVehicleState;
            c_SMSI_AVSt_03 : port SendMessagesServiceInitAM.AirVehicleState -> PlanBuilderServiceAM.AirVehicleState;
            c_SMSI_AVSt_04 : port SendMessagesServiceInitAM.AirVehicleState -> TaskManagerServiceAM.AirVehicleState;
            c_SMSI_AVSt_05 : port SendMessagesServiceInitAM.AirVehicleState -> AutomationRequestValidatorServiceAM.AirVehicleState;
            c_SMSI_AVSt_06 : port SendMessagesServiceInitAM.AirVehicleState -> RouteAggregatorServiceAM.AirVehicleState;
            c_SMSI_AVSt_07 : port SendMessagesServiceInitAM.AirVehicleState -> CmasiLineSearchTaskServiceAM.AirVehicleState;
            
	end Waterways.i;
	
end Waterways_UxAS;