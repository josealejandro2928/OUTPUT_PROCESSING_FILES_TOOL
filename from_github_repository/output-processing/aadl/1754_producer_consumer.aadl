package producer_consumer
public
  with Deployment;
    with Data_Model;

----------
-- Data --
----------

data Alpha_Type
properties
  Data_Model::Data_Representation => integer;
end Alpha_Type;

-----------------
-- Subprograms --
-----------------

subprogram Produceflow_Spg
features
  Data_Source : out parameter Alpha_Type;
properties
  source_language => (C);
  source_name     => "user_produceflow_spg";
  source_text     => ("producer-consumer-flow.c");
end Produceflow_Spg;

subprogram Consumeflow_Spg
features
  Data_Sink : in parameter Alpha_Type;
properties
  source_language => (C);
  source_name     => "user_consumeflow_spg";
  source_text     => ("producer-consumer-flow.c");
end Consumeflow_Spg;

subprogram Producevar_Spg
properties
  source_language => (C);
  source_name     => "user_producevar_spg";
  source_text     => ("producer-consumer-var.c");
end Producevar_Spg;

subprogram Consumevar_Spg
properties
  source_language => (C);
  source_name     => "user_consumevar_spg";
  source_text     => ("producer-consumer-var.c");
end Consumevar_Spg;

-------------
-- Threads --
-------------

thread producerflow
features
  Data_Source : out data port Alpha_Type;
end producerflow;



thread implementation producerflow.i
calls 
Mycalls: {
  P_Spg : subprogram Produceflow_Spg;
};
connections
  c0 : parameter P_Spg.Data_Source -> Data_Source;
properties
  Dispatch_Protocol => Periodic;
  Period            => 1000 Ms;
end producerflow.i;

thread producervar
features
  Data_Source : requires data access Alpha_Type;
end producervar;

thread implementation producervar.i
calls 
Mycalls: {
  P_Spg : subprogram Producevar_Spg;
};
properties
  Dispatch_Protocol => Periodic;
  Period            => 1000 Ms;
end producervar.i;

 
thread consumerflow
features
  Data_Sink : in data port Alpha_Type;
end consumerflow;

thread implementation consumerflow.i
calls 
Mycalls: {
  Q_Spg : subprogram Consumeflow_Spg;
};
connections
  c0 : parameter Data_Sink -> Q_Spg.Data_Sink;
properties
  Dispatch_Protocol => Periodic;
  Period            => 1000 Ms;
end consumerflow.i;

 
thread consumervar
features
  Data_Sink : requires data access Alpha_Type;
end consumervar;

thread implementation consumervar.i
calls 
Mycalls: {
  Q_Spg : subprogram Consumevar_Spg;
};
properties
  Dispatch_Protocol => Periodic;
  Period            => 1000 Ms;
end consumervar.i;

---------------
-- Processor --
---------------

processor the_processor
properties
  Deployment::Execution_Platform => Native;
end the_processor;

processor implementation the_processor.i
end the_processor.i;

---------------
-- Processes --
---------------

process mainprocess
end mainprocess;

process implementation mainprocess.flowimpl
subcomponents
  Producer        : thread producerflow.i; 
  Consumer : thread consumerflow.i;
connections
  c0 : port Producer.Data_Source -> Consumer.Data_Sink;
end mainprocess.flowimpl;

process implementation mainprocess.varimpl
subcomponents
  shareddata      : data Alpha_Type; 
  Producer        : thread producervar.i; 
  Consumer : thread consumervar.i;
connections
  c0 : data access shareddata -> Producer.Data_Source;
  c1 : data access shareddata -> Consumer.Data_Sink;
end mainprocess.varimpl;
 
------------
-- System --
------------

system pc
end pc;


system implementation pc.flowimpl
subcomponents
  pr_A : process mainprocess.flowimpl;
  CPU_A : processor the_processor.i;
  CPU_B : processor the_processor.i;
properties
  actual_processor_binding => (reference (CPU_A)) applies to pr_A;
end pc.flowimpl;


system implementation pc.varimpl
subcomponents
  pr_A : process mainprocess.varimpl;
  CPU_A : processor the_processor.i;
  CPU_B : processor the_processor.i;
properties
  actual_processor_binding => (reference (CPU_A)) applies to pr_A;
end pc.varimpl;

end producer_consumer;
