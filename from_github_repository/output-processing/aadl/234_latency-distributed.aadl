package latency_distributed
public

with base_types;
with ARINC653;
with SEI;

data int extends base_types::integer_8
end int;

thread sender
features
	outp: out data port int;
flows 
	fsource: flow source outp;
properties
	Latency => 4ms..6ms applies to fsource;
	Dispatch_Protocol => Periodic;
	Period => 50 ms;
	Deadline => 10ms; -- The deadline less than the flow spec latency is the latency contribution if schedulable.
	Compute_Execution_Time => 3 ms .. 7 ms;
end sender;


thread receiver
features
	inp: in data port int; 
flows 
	fsink: flow sink inp ;
properties
	Latency => 2ms..3ms applies to fsink;
	Dispatch_Protocol => Periodic;
	Period => 50 ms;
	Deadline => 10ms; -- The deadline less than the flow spec latency is the latency contribution if schedulable.
	Compute_Execution_Time => 3 ms .. 7 ms;
end receiver;

process psender
features
	outp: out data port int;
flows 
	fsource: flow source outp;
end psender;

process implementation psender.i
subcomponents
	task1: thread sender;
connections
	c1: port task1.outp -> outp;
flows
	fsource : flow source task1.fsource -> c1 -> outp;
end psender.i;

process preceiver
features
	inp : in data port int;
flows 
	fsink: flow sink inp ;
end preceiver;


process implementation preceiver.i
subcomponents
	task1: thread receiver;
connections
	c1 : port inp -> task1.inp;
flows
	fsink : flow sink inp -> c1 -> task1.fsink;
end preceiver.i;

virtual processor partition
end partition;

processor arinc_module
features
	thebus : requires bus access genericbus;
end arinc_module;

processor implementation arinc_module.i
subcomponents
	part1 : virtual processor partition;
	part2 : virtual processor partition;
properties
	arinc653::module_major_frame => 20ms;
	arinc653::module_schedule => ([Partition => reference (part1); Duration => 10 ms;Periodic_Processing_Start => true;],
								  [Partition => reference (part2); Duration => 10 ms;Periodic_Processing_Start => true;]
	);
end arinc_module.i;

processor genericcpu
features
	thebus : requires bus access genericbus;
end genericcpu;

bus genericbus
properties
	Latency => 1 ms .. 1 ms;
	Transmission_Time => [ Fixed => 10 ms .. 30ms;
                          PerByte => 1 us .. 10 us; ];
end genericbus;

system latencytest
end latencytest;

system implementation latencytest.generic
subcomponents
	s : process psender.i;
	r : process preceiver.i;
connections
	c0 : port s.outp -> r.inp;
flows
	etef0 : end to end flow s.fsource -> c0 -> r.fsink {Latency => 5 ms .. 10 ms;};
end latencytest.generic;

system implementation latencytest.partitioned_sei extends latencytest.generic
properties
	SEI::Is_Partition => true applies to s;
	SEI::Is_Partition => true applies to r;
	SEI::Partition_Latency => 100ms applies to s;
	SEI::Partition_Latency => 100ms applies to r;
end latencytest.partitioned_sei;

system implementation latencytest.partitioned_local extends latencytest.generic
subcomponents
	module : processor arinc_module.i;
properties
	actual_processor_binding => (reference (module.part1)) applies to s;
	actual_processor_binding => (reference (module.part2)) applies to r;
end latencytest.partitioned_local;

system implementation latencytest.partitioned_distributed extends latencytest.generic
subcomponents
	s2 : process psender.i;
	r2 : process preceiver.i;
	b : bus genericbus;
	module1 : processor arinc_module.i;
	module2 : processor arinc_module.i;
connections
	c2 : port s2.outp -> r2.inp;
	ba1 : bus access b <-> module1.thebus;
	ba2 : bus access b <-> module2.thebus;
flows
	etef1 : end to end flow s2.fsource -> c2 -> r2.fsink {Latency => 7 ms .. 15 ms;};
properties
	actual_processor_binding => (reference (module1.part1)) applies to s;
	actual_processor_binding => (reference (module2.part2)) applies to r;
	actual_processor_binding => (reference (module2.part1)) applies to s2;
	actual_processor_binding => (reference (module1.part2)) applies to r2;
	actual_connection_binding => (reference (b)) applies to c0;
	actual_connection_binding => (reference (b)) applies to c2;
end latencytest.partitioned_distributed;

system implementation latencytest.distributed extends latencytest.generic
subcomponents
	scpu : processor genericcpu;
	rcpu : processor genericcpu;
properties
	actual_processor_binding => (reference (scpu)) applies to s;
	actual_processor_binding => (reference (rcpu)) applies to r;
end latencytest.distributed;

system implementation latencytest.normal extends latencytest.distributed
subcomponents
	b : bus genericbus;
connections
	c1 : bus access b <-> scpu.thebus;
	c2 : bus access b <-> rcpu.thebus;
properties
	Actual_Connection_Binding => (reference (b)) applies to c0;
end latencytest.normal;

system implementation latencytest.nobus extends latencytest.distributed
end latencytest.nobus;


system implementation latencytest.unbound extends latencytest.distributed
subcomponents
	b : bus genericbus;
connections
	c1 : bus access b <-> scpu.thebus;
	c2 : bus access b <-> rcpu.thebus;
end latencytest.unbound;


annex description {**
	This package contains models to test the latency plugin.
	 * latencytest.normal  - contains a model to test a regular well-formed distributed system
	 * latencytest.nobus   - contains a model where there is no bus between the processor
	                         a warning should be issued
	 * latencytest.unbound - contains a model where the connection is not bound to the bus
	                         but the plugin should find the bus using the processor
	                         bus access
	 * latencytest.partitioned_distributed
	                         two senders and two receivers executed on separate partitions
	                         distributed over several processors
	 * latencytest.partitioned_local
	 						 one sender and one receiver executed on separate
	 						 partitions located on the same processor/module                         
**};
end latency_distributed;