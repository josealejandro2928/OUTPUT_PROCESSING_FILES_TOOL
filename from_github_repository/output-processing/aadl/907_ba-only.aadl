package robot_batest_01

public


with Data_Model;

data number
properties
   Data_Model::Data_Representation => Integer;
end number;

data implementation number.i
end number.i;

thread thr_producer
features
	number_out : out event data port number;
end thr_producer;

thread implementation thr_producer.i
subcomponents
	tosend : data number.i {Data_Model::Initial_Value => ("0");};
annex behavior_specification
{**
states
	init 			: initial complete final state ;
	data_sent 	    : state;
transitions
	init -[ on dispatch ]-> data_sent {tosend := tosend + 1 ; number_out!(tosend)};
	data_sent -[]-> init;
**};
end thr_producer.i;

process pr_producer
features
	number_out : out event data port number;
end pr_producer;

process implementation pr_producer.i
subcomponents
	thr : thread thr_producer.i;
connections
	c1:port thr.number_out -> number_out;
end pr_producer.i;

system sys_producer
features
	number_out : out event data port number;
end sys_producer;

system implementation sys_producer.i
subcomponents
	pr : process pr_producer.i;
connections
	c2:port pr.number_out -> number_out;
end sys_producer.i; 


thread thr_consumer
features
	number_in : in event data port number;
end thr_consumer;

thread implementation thr_consumer.i
subcomponents
	received : data number.i {Data_Model::Initial_Value => ("0");};
annex behavior_specification
{**
states
	init 			: initial complete final state ;
	data_received 	    : state;
transitions
	init -[ on dispatch number_in ]-> data_received { received := number_in};
	data_received -[]-> init;
**};
end thr_consumer.i;

process pr_consumer
features
	number_in : in event data port number;
end pr_consumer;

process implementation pr_consumer.i
subcomponents
	thr : thread thr_consumer.i;
connections
	c3:port number_in -> thr.number_in;
end pr_consumer.i;

system sys_consumer
features
	number_in : in event data port number;
end sys_consumer;

system implementation sys_consumer.i
subcomponents
	pr : process pr_consumer.i;
connections
	c4:port number_in -> pr.number_in;
end sys_consumer.i;

system main
end main;

system implementation main.i
subcomponents
	p : system sys_producer.i;
	c : system sys_consumer.i;
connections
	c5:port p.number_out -> c.number_in;
end main.i;

end robot_batest_01;