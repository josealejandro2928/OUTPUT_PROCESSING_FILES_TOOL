----------------------------------------------
--  IoT example
--       Julien Delange <jdelange@sei.cmu.edu>
--
-- The iot::home::server::integration captures
-- the integration of the web server that is
-- connected with the outside world. The outside
-- world sends request, the server filters
-- them and finally forwards them to the sap_node.
----------------------------------------------


package iot::home::server::integration
public
	with iot::home::server::platform;
	with iot::home::server::software;
	with iot::common::platform;
with iot::common::datatypes;
	
	system server
	features
		eth 				: requires bus access iot::common::platform::ethernet;	
		user_request 		: in event data port iot::common::datatypes::request;
		user_reply 			: out event data port iot::common::datatypes::reply;
		internal_request 	: out event data port iot::common::datatypes::internal_request;
		internal_reply :	 in event data port iot::common::datatypes::internal_reply;
	flows
		server_flow_request : flow path user_request -> internal_request;
		server_flow_reply   : flow path internal_reply -> user_reply;
	end server;
	
	system implementation server.i
	subcomponents
		cpu  : processor iot::home::server::platform::linux;
		soft : process iot::home::server::software::server;
	connections
		user_req     : port user_request -> soft.user_request;
		user_rep     : port soft.user_reply -> user_reply;
		int_request  : port soft.internal_request -> internal_request;
		int_reply    : port internal_reply -> soft.internal_reply;
		
		eth_conn     : bus access eth <-> cpu.eth;
	flows
		server_flow_request  : flow path user_request -> user_req -> soft.request_flow -> int_request -> internal_request;
		server_flow_reply    : flow path internal_reply -> int_reply -> soft.reply_flow -> user_rep -> user_reply;
	properties
		actual_processor_binding => (reference (cpu)) applies to soft;
	end server.i;
end iot::home::server::integration;
 