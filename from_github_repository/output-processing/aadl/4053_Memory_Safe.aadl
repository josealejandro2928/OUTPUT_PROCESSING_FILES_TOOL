package Memory_Safe
  public
    annex resolute {**
	
memory_safe() <=
	** "The system is memory safe" **
	forall (p : process). memory_protected(p)

memory_protected(p : process) <=
  ** "The memory of process " p " is protected from alterations by other processes" **
  os_guarantees_memory_safety(p) or
  (os_guarantees_memory_separation(p) and
   forall (mem : memory). bound(p, mem) =>
     forall (q : process). bound(q, mem) => memory_safe_process(q))

os_guarantees_memory_safety(p : process) <=
  ** "The operating system of " p " guarantees memory safety" **
  runs_on_os(p, "SeL4")

os_guarantees_memory_separation(p : process) <=
  ** "The operating system of " p " guarantees thread local memory is separated" **
  runs_on_os(p, "eChronos")

runs_on_os(p : process, os : string) <=
  ** p " runs on " os **
  property(p, SMACCM::OS) = os

memory_safe_process(p : process) <=
  ** "The process " p " only writes to its own memory space" **
  forall (t : thread). contained(t, p) => memory_safe_thread(t)

memory_safe_thread(t : thread) <=
  ** "The thread " t " only writes to its own memory space" **
  ivory_thread(t)
  
ivory_thread(t : thread) <=
  ** "The thread " t " is generated from Ivory" **
  has_property(t, SMACCM::Language) and
  property(t, SMACCM::Language) = "Ivory"

**};
	
end Memory_Safe;