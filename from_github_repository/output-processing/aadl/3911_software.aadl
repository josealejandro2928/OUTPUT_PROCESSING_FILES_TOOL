package emsoft2016::solutions::exercise3::software
public
	with emsoft2016::solutions::exercise1::datamodel;
	with emsoft2016::solutions::exercise2::software;

	--
	--  We extend the description from the previous example and bring the capability
	--  to interface with the devices from the outside. Here, we want to have the capability
	--  to be able to interface with the sensors.
	--

	thread filtering_thr extends emsoft2016::solutions::exercise2::software::filtering_thr
	flows
		f0 : flow path raw_value0 -> corrected_value;
		f1 : flow path raw_value1 -> corrected_value;
	end filtering_thr;

	process filtering extends emsoft2016::solutions::exercise2::software::filtering
	flows
		f0 : flow path raw_value0 -> corrected_value;
		f1 : flow path raw_value1 -> corrected_value;
	end filtering;
	
	process implementation filtering.i extends emsoft2016::solutions::exercise2::software::filtering.i
	subcomponents
		thr : refined to thread filtering_thr;
	flows
		f0 : flow path raw_value0 -> c1 -> thr.f0 -> c0 -> corrected_value;
		f1 : flow path raw_value1 -> c2 -> thr.f1 -> c0 -> corrected_value;
	end filtering.i;
	
	--
	--  We extend the description from the previous example and bring the capability
	--  to interface with the devices from the outside. Here, we want to have the capability
	--  to be able to interface with the heater and cooler.
	--
	
	thread air_conditioner_thr extends emsoft2016::solutions::exercise2::software::air_conditioner_thr
	flows
		f0 : flow path corrected_value -> activate_cooler;
		f1 : flow path corrected_value -> activate_heater;
	end air_conditioner_thr;
	
	process air_conditioner extends emsoft2016::solutions::exercise2::software::air_conditioner
	flows
		f0 : flow path corrected_value -> activate_cooler;
		f1 : flow path corrected_value -> activate_heater;
	end air_conditioner;
	
	process implementation air_conditioner.i extends emsoft2016::solutions::exercise2::software::air_conditioner.i
	subcomponents
		thr : refined to thread air_conditioner_thr;
	flows
		f0 : flow path corrected_value -> c0 -> thr.f0 -> c1 -> activate_cooler;
		f1 : flow path corrected_value -> c0 -> thr.f1 -> c2 -> activate_heater;
	end air_conditioner.i;

end emsoft2016::solutions::exercise3::software;
