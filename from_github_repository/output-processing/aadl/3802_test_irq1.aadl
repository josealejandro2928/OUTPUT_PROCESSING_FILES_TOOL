-- Note: this example shows a single send of a scalar type from an
-- active thread to a passive thread.  It is designed to test:
-- 	Component naming
--  Message distribution for scalars
--  Passive thread dispatch 


package test_irq1
public 
   with Base_Types;
   with TB_SYS;   

-- Example of a periodically dispatched "Active" thread.   
thread sender
	features
		Input1: in event data port Base_Types::Integer_64 {
		  Source_Text => ("user_code/user_sender.c",
		  				  "user_code/user_main.c", 
		                  "user_code/pixhawk_clock_driver.c", 
		                  "user_code/clock_driver.h");
		  TB_SYS::Is_ISR => true; 
		  TB_SYS::First_Level_Interrupt_Handler => "timer_flih";
	      Compute_Entrypoint_Source_Text => "timer_slih";
		  TB_SYS::Signal_Name => "systick";
	      TB_SYS::Sends_Events_To => "{{2 Output1}}";
		  TB_SYS::Memory_Pages => ("mem", "0x53F98000:0x1000");
		  --TB_SYS::Signal_Number => 27;			
		};
		Output1: out event data port Base_Types::Unsigned_32 {
	      TB_SYS::CommPrim_Source_Text => "ping_Output1";			
		};
	
	properties 
	    Dispatch_Protocol => Sporadic;
	    Period => 100 ms;
	    Initialize_Entrypoint_Source_Text => "initialize_timer";
	    TB_SYS::Sends_Events_To => "{{}}";
	    Priority => 10;
	    Stack_Size => 512 bytes;
	    TB_SYS::Thread_Type => Active ;
	    Compute_Execution_Time => 10 us .. 50 us;
end sender ; 

-- Example of an aperiodically dispatched "passive" thread.   
thread receiver
	features
		Input1: in event data port Base_Types::Unsigned_32 {
	      TB_SYS::Compute_Entrypoint_Source_Text => ("ping_received");
		  Source_Text => ("user_code/user_receiver.c");
		}; 
				
	properties 
	    Dispatch_Protocol => Aperiodic;
	    TB_SYS::Thread_Type => Passive ;
	    Compute_Execution_Time => 10 us .. 50 us;
	    TB_SYS::Sends_Events_To => "{{}}";
end receiver ; 


process proc

end proc;

process implementation proc.Impl
	subcomponents
		s: thread sender;
		r1: thread receiver;
	connections
		s_to_r1 : port s.Output1 -> r1.Input1;
end proc.Impl;

system test_irq1
end test_irq1;

system implementation test_irq1.impl
	subcomponents
		tl: process proc.Impl;
	properties
		TB_SYS::Generate_Scheduler_Systick_IRQ => false;
		TB_SYS::OS => eChronos; 
		TB_SYS::HW => PIXHAWK;
		Source_Text => ("user_code/user_main.c");
end test_irq1.impl;

end test_irq1;