package FHAExample
public
with SEI, MILSTD882, EMV2;
with ErrorLibrary;
-- use of Hazard property both in generic form (EMV2) and in one of the standard forms (EMV2::).
-- Generic form uses enumeration A-E for severity levels, and integer 1-5 for likelihood
-- Standard specific variants (EMV2:: and ARP4761) use enumerations for severity and likelihood.
-- illustrates use of symbolic constants in generic hazard form to map standard specific labels into base type.
-- it also illustrates to use of the hazard property applied to the state as error source, a type as error source, or the error propagation as error source.
-- finally, it illustrates associating the hazard property in the context of a component classifier and in the context of a subcomponent.
  device PositionSensor
    features
      PositionReading: out data port ;
    flows
      f1: flow source PositionReading {
        Latency => 2 ms .. 3 ms;
        };
    annex EMV2 {**
        use types ErrorLibrary;
    	use behavior ErrorLibrary::FailStop; 
    	error propagations 
    		PositionReading: out propagation  {ServiceOmission,ValueError,ItemOmission};
    	flows
    	
		 ef1:error source PositionReading {ServiceOmission} when FailStop;
		 ef2:error source PositionReading {ValueError} when FailStop;
		 ef3:error source PositionReading {ItemOmission} when FailStop;
	end propagations;
	properties
	-- property is associated with the error behavior state as the source of an error source.
	-- it uses the generic version of the Hazard property

	EMV2::hazards => 
	([	crossreference => "1.1.1";
		failure => "Loss of sensor readings";
		phases => ("all");
		severity => ARP4761::Hazardous;
		likelihood => ARP4761::Remote;
		description => "No position readings due to sensor failure";
		comment => "Becomes major hazard, if no rdeundant sensor";
			])
			applies to ef1;
    **};
  end PositionSensor;

  
  
  device Actuator
    features
      ActCmd: in data port ;
    flows
      fs: flow sink ActCmd {
        Latency => 2 ms .. 3 ms;
        };
    annex EMV2 {**
        use types ErrorLibrary;
    	use behavior ErrorLibrary::FailStop;
    	error propagations 
    	    operation: out propagation {ServiceOmission};
    		ActCmd: in propagation  {ServiceOmission,ValueError,ItemOmission};
    	flows
		 ef1:error sink ActCmd{ValueError};
		 ef2:error source operation {ServiceOmission} when FailStop;
	
 		end propagations;
	  	propagation paths
 			operation: propagation point;
 		end paths;
	properties
	-- the property is associated with an a specific error type of an outgoing observable propagation point
	-- it uses the EMV2:: specific variant of the hazard property, the severity and likelihood values are enumeration literals.
		EMV2::severity => MILSTD882::Catastrophic applies to operation.ServiceOmission;
		EMV2::likelihood => MILSTD882::Remote applies to operation.ServiceOmission;
	EMV2::hazards => (
	[	crossreference => "1.1.2";
		failure => "Loss of actuator functionality";
		phases => ("all");
		severity => ARP4761::NoEffect;
		likelihood => ARP4761::ExtremelyRemote;
		description => "Failure to move actuator into desired position";
		comment => "Is a major hazard. Pilot cannot control actuator due to sensor failure";
			])
			applies to operation.ServiceOmission;
    **};
  end Actuator;
  
  
  
  processor PowerPC
    properties
      SEI::MIPSCapacity => 5.0 MIPS;
  end PowerPC;
  

  system AircraftFMS
  end AircraftFMS;
  
  system implementation AircraftFMS.i
    subcomponents
      PositionSensor: device PositionSensor;
      Actuator1: device Actuator ;
      Actuator2: device Actuator ;
      FMSProcessor: processor PowerPC;
    connections
      sensedPosition: port PositionSensor.PositionReading  -> Actuator1.ActCmd;
      Actuator2Cmd: port PositionSensor.PositionReading -> Actuator2.ActCmd;
annex EMV2 {** 
	properties 
	-- here the hazard property is associated with the error behavior state in an error source of a subcomponent.
	-- In this case the path starts out within the core model (subcomponent)
	-- It overrides the Hazard property assocaited in the component classifier of the subcomponent.
		EMV2::severity => MILSTD882::Critical applies to ^Actuator2@ef2.ServiceOmission;
		EMV2::likelihood => MILSTD882::Remote applies to ^Actuator2@ef2.ServiceOmission;
		EMV2::hazards => 
	([	crossreference => "1.1.3";
		failure => "Loss of actuator action";
		phases => ("all");
		severity => ARP4761::NoEffect;
		likelihood => ARP4761::ExtremelyRemote;
		description => "No action due to sensor failure";
		comment => "Becomes major hazard, if no rdeundant sensor";
			])
			applies to ^Actuator2@ef2;
	**}; 

  end AircraftFMS.i;
end FHAExample;