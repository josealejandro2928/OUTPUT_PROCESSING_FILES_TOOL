package TA6_Demonstration_UxAS
public
	with CASE_Messages;
	with CMASI;
	
	-- Included Main Services
	with TaskManagerService_Pkg;
	with ZeroizeConditionRecognizer_Pkg;
	with AutoPilotAdapterService_Pkg;
	with CommunicationsAdapterService_Pkg;


	with UxAS;
		
	process CASE_Sys extends UxAS::UxAS
		features
			KeepInZone : in data port CMASI::KeepInZone.i;
			KeepOutZone : in data port CMASI::KeepOutZone.i;
			OperatingRegion : in data port CMASI::OperatingRegion.i;
			ZeroizeCondition : in data port CASE_Messages::ZeroizeCondition.i;
			AirVehicleState : in data port CMASI::AirVehicleState.i;
			
			AP_IO: feature group;
			Controller_IO: feature group;
			Engine_IO: feature group;
			EO_IR_Video: feature group;
			EO_IR_Data_and_Control:  feature group;
			Configuration_IO: feature group; 			
	end CASE_Sys;
	
	process implementation CASE_Sys.Demo
		subcomponents
  			TaskManagerService: thread TaskManagerService_Pkg::TaskManagerService;
  			ZeroizeConditionRecognizerService : thread ZeroizeConditionRecognizer_Pkg::ZeroizeConditionRecognizerService;
  			AutopilotAdapterService: thread AutoPilotAdapterService_Pkg::AutopilotAdapterService;
  			CommAdapterService: thread CommunicationsAdapterService_Pkg::CommAdapterService;  			  			
  			
		connections
			
			------------------------------------------------------------------------------------------------------------------
			-- Connections between the ground station (or controller) and the UxAS instances, and connections between UxAS
			-- services. The connections reflect the message sequence flow defined for this example. The message sequence flow 
			-- has 4 states: Initialization, Assignment, Implementation, and Execution. The connections are group and labeled 
			-- based on the state(s) in which the messages occur.
			------------------------------------------------------------------------------------------------------------------
			
			------------------
			-- Initialization
			------------------
			c_init_01 : port KeepInZone -> ZeroizeConditionRecognizerService.KeepInZone;			
			c_init_02 : port KeepOutZone -> ZeroizeConditionRecognizerService.KeepOutZone;			
			c_init_03 : port OperatingRegion -> ZeroizeConditionRecognizerService.OperatingRegion;
			
			--------------
			-- Assignment
			--------------			
			-- none
       		
        	-------------------
       		-- Implementation
       		-------------------
  			c_impl_01 : port ZeroizeCondition -> ZeroizeConditionRecognizerService.ZeroizeCondition;  			
			c_impl_02 : port AirVehicleState -> ZeroizeConditionRecognizerService.AirVehicleState;
        	c_impl_03 : port AirVehicleState -> TaskManagerService.AirVehicleState;
        		
       		-------------
       		-- Execution
       		-------------
       		c_exec_01 : port ZeroizeConditionRecognizerService.ZeroizeCommand -> TaskManagerService.ZeroizeCommand;
       		c_exec_02 : feature group CommAdapterService.Ground_Controller_IO <-> Controller_IO;
       		c_exec_03 : feature group AutopilotAdapterService.Autopilot_Cmds <-> AP_IO; 
       		
				
	end CASE_Sys.Demo;	
end TA6_Demonstration_UxAS;